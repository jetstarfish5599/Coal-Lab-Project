TITLE Bordered Snake Game (MASM / Irvine32)
;Project by Masoom Khan, Dev Kumar, Sagbatullah
INCLUDE Irvine32.inc

; Constants 
MIN_X = 1
MAX_X = 78
MIN_Y = 1
MAX_Y = 23
MAX_LENGTH = 200

;direction constants
DIR_RIGHT = 0
DIR_LEFT  = 1
DIR_UP    = 2
DIR_DOWN  = 3

.data
snakeBody    COORD MAX_LENGTH dup( <> ) 
snakeLength  DWORD 5
snakeDirection DWORD DIR_RIGHT
foodPosition COORD <>
score        DWORD 0
gameOver     DWORD 0
gameDelay    DWORD 100
oldTail      COORD <>
ateFoodFlag  DWORD 0

snakeChar    BYTE 'O'
foodChar     BYTE '@'
borderChar   BYTE '#'
scoreMsg     BYTE "Score: ",0
gameOverMsg1 BYTE "Game Over!",0
gameOverMsg2 BYTE "Press any key to exit.",0

.code
main PROC
    call InitializeGame

gameLoop:
    cmp gameOver, 1
    je  showGameOverScreen

    call HandleInput
    call UpdateGame
    call DrawGame
    mov  eax, gameDelay
    call Delay
    jmp  gameLoop

showGameOverScreen:
    call DisplayGameOver
    call WaitMsg
    exit
main ENDP

;-------------------------------
InitializeGame PROC
    push ebp
    mov  ebp, esp

    call ClrScr
    call Randomize
    
    mov  gameOver, 0
    mov  score, 0
    mov  snakeLength, 5
    mov  snakeDirection, DIR_RIGHT

    ; initial snake
    mov  snakeBody[0 * TYPE COORD].X, 10
    mov  snakeBody[0 * TYPE COORD].Y, 10
    mov  snakeBody[1 * TYPE COORD].X, 9
    mov  snakeBody[1 * TYPE COORD].Y, 10
    mov  snakeBody[2 * TYPE COORD].X, 8
    mov  snakeBody[2 * TYPE COORD].Y, 10
    mov  snakeBody[3 * TYPE COORD].X, 7
    mov  snakeBody[3 * TYPE COORD].Y, 10
    mov  snakeBody[4 * TYPE COORD].X, 6
    mov  snakeBody[4 * TYPE COORD].Y, 10

    call DrawBorder
    call PlaceFood
    call UpdateScoreDisplay

    pop  ebp
    ret
InitializeGame ENDP

;-------------------------------
HandleInput PROC
    push ebp
    mov  ebp, esp
    push eax
    push edx

    call ReadKey
    cmp  eax, 0
    je   no_input

    cmp  ah, 48h
    je   tryUp
    cmp  ah, 50h
    je   tryDown
    cmp  ah, 4Bh
    je   tryLeft
    cmp  ah, 4Dh
    je   tryRight
    jmp  no_input

tryUp:
    mov  edx, snakeDirection
    cmp  edx, DIR_DOWN
    je   no_input
    mov  snakeDirection, DIR_UP
    jmp  no_input

tryDown:
    mov  edx, snakeDirection
    cmp  edx, DIR_UP
    je   no_input
    mov  snakeDirection, DIR_DOWN
    jmp  no_input

tryLeft:
    mov  edx, snakeDirection
    cmp  edx, DIR_RIGHT
    je   no_input
    mov  snakeDirection, DIR_LEFT
    jmp  no_input

tryRight:
    mov  edx, snakeDirection
    cmp  edx, DIR_LEFT
    je   no_input
    mov  snakeDirection, DIR_RIGHT

no_input:
    pop edx
    pop eax
    pop ebp
    ret
HandleInput ENDP

;-------------------------------
UpdateGame PROC
    push ebp
    mov  ebp, esp

    call UpdateSnake
    call CheckFoodCollision
    call CheckWallCollision
    call CheckSelfCollision

    pop  ebp
    ret
UpdateGame ENDP

;-------------------------------
UpdateSnake PROC
    push ebp
    mov  ebp, esp
    pushad

    ;save tail
    mov  ecx, snakeLength
    dec  ecx
    mov  esi, OFFSET snakeBody
    lea  esi, [esi + (ecx * TYPE COORD)]
    mov  ax, (COORD PTR [esi]).X
    mov  bx, (COORD PTR [esi]).Y
    mov  oldTail.X, ax
    mov  oldTail.Y, bx

    ;shift body
    mov  ecx, snakeLength
    dec  ecx
    jz   moveHead

    lea  edi, [esi]
    lea  esi, [esi - TYPE COORD]
    std
    rep movsd
    cld

moveHead:
    mov  esi, OFFSET snakeBody
    mov  ax, (COORD PTR [esi]).X
    mov  bx, (COORD PTR [esi]).Y

    mov  ecx, snakeDirection
    cmp  ecx, DIR_RIGHT
    je   moveRight
    cmp  ecx, DIR_LEFT
    je   moveLeft
    cmp  ecx, DIR_UP
    je   moveUp
    cmp  ecx, DIR_DOWN
    je   moveDown

moveRight: inc ax
           jmp setNewHead
moveLeft:  dec ax
           jmp setNewHead
moveUp:    dec bx
           jmp setNewHead
moveDown:  inc bx

setNewHead:
    mov  (COORD PTR [esi]).X, ax
    mov  (COORD PTR [esi]).Y, bx

    popad
    pop  ebp
    ret
UpdateSnake ENDP

;-------------------------------
CheckFoodCollision PROC
    push ebp
    mov  ebp, esp
    pushad

    mov  ateFoodFlag, 0
    mov  esi, OFFSET snakeBody
    mov  ax, (COORD PTR [esi]).X
    mov  bx, (COORD PTR [esi]).Y

    cmp  ax, foodPosition.X
    jne  noEat
    cmp  bx, foodPosition.Y
    jne  noEat

    mov  ateFoodFlag, 1
    inc  snakeLength
    add  score, 10
    call UpdateScoreDisplay
    call PlaceFood

noEat:
    popad
    pop  ebp
    ret
CheckFoodCollision ENDP

;-------------------------------
CheckWallCollision PROC
    push ebp
    mov  ebp, esp
    pushad

    mov  esi, OFFSET snakeBody
    mov  ax, (COORD PTR [esi]).X
    mov  bx, (COORD PTR [esi]).Y

    cmp  ax, MIN_X
    jl   setGameOver
    cmp  ax, MAX_X
    jg   setGameOver
    cmp  bx, MIN_Y
    jl   setGameOver
    cmp  bx, MAX_Y
    jg   setGameOver
    jmp  noWallCollision

setGameOver:
    mov  gameOver, 1

noWallCollision:
    popad
    pop  ebp
    ret
CheckWallCollision ENDP

;-------------------------------
CheckSelfCollision PROC
    push ebp
    mov  ebp, esp
    pushad

    mov  esi, OFFSET snakeBody
    mov  ax, (COORD PTR [esi]).X
    mov  bx, (COORD PTR [esi]).Y

    add  esi, TYPE COORD
    mov  ecx, snakeLength
    dec  ecx

checkLoop:
    cmp  ecx, 0
    je   noSelfCollision
    cmp  ax, (COORD PTR [esi]).X
    jne  nextSegment
    cmp  bx, (COORD PTR [esi]).Y
    jne  nextSegment

    mov  gameOver, 1
    jmp  noSelfCollision

nextSegment:
    add  esi, TYPE COORD
    loop checkLoop

noSelfCollision:
    popad
    pop  ebp
    ret
CheckSelfCollision ENDP

;-------------------------------
PlaceFood PROC
    push ebp
    mov  ebp, esp
    pushad

spawnLoop:
    mov  eax, MAX_X - MIN_X - 1
    call RandomRange
    add  eax, MIN_X + 1
    mov  foodPosition.X, ax

    mov  eax, MAX_Y - MIN_Y - 1
    call RandomRange
    add  eax, MIN_Y + 1
    mov  foodPosition.Y, ax

    mov  ax, foodPosition.X
    mov  bx, foodPosition.Y
    mov  ecx, snakeLength
    mov  esi, OFFSET snakeBody
checkSnakePos:
    cmp  ax, (COORD PTR [esi]).X
    jne  posOK
    cmp  bx, (COORD PTR [esi]).Y
    jne  posOK
    jmp  spawnLoop

posOK:
    add  esi, TYPE COORD
    loop checkSnakePos

    mov  ax, foodPosition.Y
    mov  dh, al
    mov  ax, foodPosition.X
    mov  dl, al
    call Gotoxy
    mov  al, foodChar
    call WriteChar

    popad
    pop  ebp
    ret
PlaceFood ENDP

;-------------------------------
DrawGame PROC
    push ebp
    mov  ebp, esp
    pushad

    ;draw head
    mov  esi, OFFSET snakeBody
    mov  ax, (COORD PTR [esi]).Y
    mov  dh, al
    mov  ax, (COORD PTR [esi]).X
    mov  dl, al
    call Gotoxy
    mov  al, snakeChar
    call WriteChar

    ;erase tail if no growth
    cmp  ateFoodFlag, 1
    je   skipErase

    mov  ax, oldTail.Y
    mov  dh, al
    mov  ax, oldTail.X
    mov  dl, al
    call Gotoxy
    mov  al, ' '
    call WriteChar

skipErase:
    popad
    pop  ebp
    ret
DrawGame ENDP

;-------------------------------
DrawBorder PROC
    push ebp
    mov  ebp, esp
    pushad
    
    mov  al, borderChar

    mov  ecx, MAX_X - MIN_X + 1
    mov  dl, MIN_X
    mov  dh, MIN_Y
    call Gotoxy
drawTop:
    call WriteChar
    loop drawTop
    
    mov  dl, MIN_X
    mov  dh, MAX_Y
    call Gotoxy
    mov  ecx, MAX_X - MIN_X + 1
drawBottom:
    call WriteChar
    loop drawBottom

    mov  ecx, MAX_Y - MIN_Y - 1
    mov  dl, MIN_X
    mov  dh, MIN_Y + 1
drawLeft:
    call WriteChar
    inc  dh
    call Gotoxy
    loop drawLeft

    mov  ecx, MAX_Y - MIN_Y - 1
    mov  dl, MAX_X
    mov  dh, MIN_Y + 1
drawRight:
    call WriteChar
    inc  dh
    call Gotoxy
    loop drawRight

    popad
    pop  ebp
    ret
DrawBorder ENDP

;-------------------------------
UpdateScoreDisplay PROC
    push ebp
    mov  ebp, esp
    pushad
    
    mov  dh, 0
    mov  dl, 0
    call Gotoxy
    mov  edx, OFFSET scoreMsg
    call WriteString
    mov  eax, score
    call WriteDec
    call WriteString
    
    popad
    pop  ebp
    ret
UpdateScoreDisplay ENDP

;-------------------------------
DisplayGameOver PROC
    push ebp
    mov  ebp, esp
    pushad
    
    mov  dh, 10
    mov  dl, 30
    call Gotoxy
    mov  edx, OFFSET gameOverMsg1
    call WriteString

    mov  dh, 11
    mov  dl, 25
    call Gotoxy
    mov  edx, OFFSET gameOverMsg2
    call WriteString
    
    popad
    pop  ebp
    ret
DisplayGameOver ENDP

END main
